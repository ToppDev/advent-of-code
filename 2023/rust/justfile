# Use `just work day-01 part1` to work on the specific binary for a specific day's problems
work day part:
    cargo watch -w {{day}} -x "check -p {{day}}" -s "just test {{day}} {{part}}" -s "just lint {{day}}"
# Run clippy for a specific day
lint day:
    cargo clippy -p {{day}}
# Run tests for a specific day and part
test day part:
    cargo nextest run -p {{day}} {{part}}
# Benchmark all days
bench-all:
    cargo bench -q > benchmarks.txt
# Benchmark a specifiv day and part
bench day part:
    cargo bench --bench {{day}}-bench {{part}}
# Create a flamegraph for a specific day and part
flamegraph day part:
    cargo flamegraph --profile flamegraph --root --package {{day}} --bin {{part}} -o flamegraphs/{{day}}--{{part}}.svg
# Use `just create day-01` to create a new day from the template
create day:
    cargo generate --path ./daily-template --name {{day}}
